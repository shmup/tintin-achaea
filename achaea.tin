#nop https://tintin.mudhalla.net/manual/
#nop https://tintinplusplus-unoffical-documentation.readthedocs.io/

#event {PROGRAM START} {connect}

#alias connect {#session achaea achaea.com 23;}
#alias reload  {#kill all; #read achaea.tin; #config {command echo} {on}}
#act {Password correct. Welcome to Achaea.} {#config {command echo} {on}}

#prompt {%1h, %2m, %3e, %4w %5-} {
  <fff>  â–¶
  <ade><188>%1
  <baf><188>%2
  <eea><188>%3
  <fca><188>%4
  <afc><188>%5
  <fff>
  <088>
}

#alias eld {eldritchmists}
#alias dir {directory}
#alias sl {show level}
#alias ll {info here}
#alias q {#if {%1} {quest %1 details}; #else quest log;}

#split 1 1
#config {repeat char}   {!}
#config {repeat enter}  {off}
#config {tintin char}   {#}
#config {command echo}  {off}
#config {speedwalk}     {off}
#config {verbatim char} {\}
#config {log}           {html}
#config {mccp}          {on}
#config {buffer size}   {1000000}
#config {history size}  {2000}
#config {wordwrap} on

#nop CAW! https://freesound.org/people/inchadney/sounds/124925/
#alias caw {#system {afplay assets/caw1.wav}}
#foreach {
  {You cease your meditation having achieved full will and mana.}
  {Your mental prowess is already swollen to its maximum.}
  {You open your eyes and stretch languidly, feeling deliciously well-rested.}
  {You open your eyes and yawn mightily.}
} {msg} {
    #line substitute variables #act {$msg} {caw};
}

#alias hud {
  #showme {-----------------};
  show level;
  show gold;
  areas;
}

#act {You will TIMEOUT in 1 minute unless you do something.} {
  hud;
}

#nop needed for highlighting
#nop https://sites.google.com/site/chanizylvetea/
#config {color} {on}
#gag {~^\e[0;1;[30|31|32|33|34|35|36|37]m\e[1;[30|31|32|33|34|35|36|37]m^}
#gag {~^\e[0;[30|31|32|33|34|35|36|37]m\e[[30|31|32|33|34|35|36|37]m^}

#alias music {#system {afplay $(find -s soundscapes -name "*.mp3" | fzy) &}}

#nop http://ix.io/2MoR/
